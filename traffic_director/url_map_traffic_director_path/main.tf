/**
 * Copyright 2022 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

# [START trafficdirector_url_map_traffic_director_path]
resource "google_compute_url_map" "urlmap" {
  name            = "urlmap"
  description     = "a description"
  default_service = google_compute_backend_service.home.id

  host_rule {
    hosts        = ["mysite.com"]
    path_matcher = "allpaths"
  }

  path_matcher {
    name            = "allpaths"
    default_service = google_compute_backend_service.home.id

    path_rule {
      paths = ["/home"]
      route_action {
        cors_policy {
          allow_credentials    = true
          allow_headers        = ["Allowed content"]
          allow_methods        = ["GET"]
          allow_origin_regexes = ["abc.*"]
          allow_origins        = ["Allowed origin"]
          expose_headers       = ["Exposed header"]
          max_age              = 30
          disabled             = false
        }
        fault_injection_policy {
          abort {
            http_status = 234
            percentage  = 5.6
          }
          delay {
            fixed_delay {
              seconds = 0
              nanos   = 50000
            }
            percentage = 7.8
          }
        }
        request_mirror_policy {
          backend_service = google_compute_backend_service.home.id
        }
        retry_policy {
          num_retries = 4
          per_try_timeout {
            seconds = 30
          }
          retry_conditions = ["5xx", "deadline-exceeded"]
        }
        timeout {
          seconds = 20
          nanos   = 750000000
        }
        url_rewrite {
          host_rewrite        = "A replacement header"
          path_prefix_rewrite = "A replacement path"
        }
        weighted_backend_services {
          backend_service = google_compute_backend_service.home.id
          weight          = 400
          header_action {
            request_headers_to_remove = ["RemoveMe"]
            request_headers_to_add {
              header_name  = "AddMe"
              header_value = "MyValue"
              replace      = true
            }
            response_headers_to_remove = ["RemoveMe"]
            response_headers_to_add {
              header_name  = "AddMe"
              header_value = "MyValue"
              replace      = false
            }
          }
        }
      }
    }
  }

  test {
    service = google_compute_backend_service.home.id
    host    = "hi.com"
    path    = "/home"
  }
}

resource "google_compute_backend_service" "home" {
  name        = "home"
  port_name   = "http"
  protocol    = "HTTP"
  timeout_sec = 10

  health_checks         = [google_compute_health_check.default.id]
  load_balancing_scheme = "INTERNAL_SELF_MANAGED"
}

resource "google_compute_health_check" "default" {
  name = "health-check"
  http_health_check {
    port = 80
  }
}
# [END trafficdirector_url_map_traffic_director_path]
